apply plugin: "java"
apply plugin: "maven"
apply plugin: "application"
apply plugin: "idea"
apply plugin: "cpp"
apply plugin: "signing"

ext {
    release = hasProperty("release") ? release : false
    signEnabled = hasProperty("signEnabled") ? signEnabled : false
    sonatypeUsername = hasProperty("sonatypeUsername") ? sonatypeUsername : "";
    sonatypePassword = hasProperty("sonatypePassword") ? sonatypePassword : "";

    openZWaveDir = project.openZWaveDir
    openZWaveLibFile = project.openZWaveLibFile
    openZWaveConfigDir = project.openZWaveConfigDir
    zWaveControllerPort = project.zWaveControllerPort

    javaHome = System.getenv("JAVA_HOME")
    nativeClasses = ["org.zwave4j.Manager", "org.zwave4j.Options"]

    additionalNativeLibsDir = "${projectDir}/native_libs"

    srcNativeLibsDir = "native_libs"

    def osName = System.properties["os.name"]
    if (osName == "Linux") {
        os = "linux"
    } else if (osName.startsWith("Windows")) {
        os = "windows"
    } else if (osName == "SunOS") {
        os = "solaris"
    } else if (osName.endsWith("OS X")) {
        os = "os_x"
    }

    def architectureName = System.properties["os.arch"]
    if (architectureName.endsWith("86")) {
        architecture = "x86"
    } else if (architectureName == "amd64" || architectureName == "x86_64") {
        architecture = "amd64"
    } else if (architectureName == "arm") {
        architecture = "arm"
    }
}

group = "com.github.zgmnkv"
version = "0.2" + (release ? "" : "-SNAPSHOT")
mainClassName = "org.zwave4j.Main"

repositories {
    mavenLocal()
    mavenCentral()
}

toolChains {
    gcc(Gcc) {}
}

libraries {
    main {
        baseName = "zwave4j"
        binaries.all {
            if (toolChain in Gcc) {
                cppCompiler.args "-I", "${javaHome}/include"
                cppCompiler.args "-I", "${javaHome}/include/win32"
                cppCompiler.args "-I", "${javaHome}/include/linux"
                cppCompiler.args "-I", "${openZWaveDir}/cpp/src"
                cppCompiler.args "-I", "${openZWaveDir}/cpp/src/value_classes"
                cppCompiler.args "-I", "${openZWaveDir}/cpp/src/command_classes"
                cppCompiler.args "-I", "${openZWaveDir}/cpp/src/platform"
                cppCompiler.args "-I", "${openZWaveDir}/cpp/src/platform/unix"
                cppCompiler.args "-I", "${openZWaveDir}/cpp/src/platform/windows"

                if (architecture == "amd64") {
                    cppCompiler.args "-m64"
                    toolChain.linker.withArguments { args ->
                        args << "-m64"
                    }
                }
                if (architecture == "x86") {
                    cppCompiler.args "-m32"
                    toolChain.linker.withArguments { args ->
                        args << "-m32"
                    }
                }
            
                if (os == "windows") {
                    linker.args "-static"
                    toolChain.linker.withArguments { args ->
                            args << "-lsetupapi"
                    }
                    if (architecture == "x86") {
                        linker.args "--kill-at"
                    }
                } 
                
                toolChain.linker.withArguments { args ->
                    args << project.openZWaveLibFile
                }
                
                tasks.withType(CppCompile) {
                    dependsOn generateNativeHeaders
                }
            }
        }
    }
}

task generateNativeHeaders(type: Exec, dependsOn: classes) {
    inputs.dir(sourceSets.main.output.classesDir)
    commandLine "javah"
    
    afterEvaluate {
        outputs.dir(sources.main.cpp.exportedHeaders.srcDirs.iterator()[0])
        args "-d", sources.main.cpp.exportedHeaders.srcDirs.iterator()[0]
        args "-classpath", sourceSets.main.runtimeClasspath.asPath
        args nativeClasses
    }
}

task copyNativeLibs(type: Copy, dependsOn: ":mainSharedLibrary") {
    into "${sourceSets.main.output.classesDir}/${srcNativeLibsDir}"
    from "${additionalNativeLibsDir}"
    from "${buildDir}/binaries/mainSharedLibrary", {
        into "${os}/${architecture}"
    }
}

jar.dependsOn copyNativeLibs

run {
    dependsOn copyNativeLibs
    standardInput = System.in
    jvmArgs([
            "-Xdebug",
            "-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=5005"
    ])
    args([
            openZWaveConfigDir,
            zWaveControllerPort
    ])
}

task sourcesJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier "javadoc"
    from javadoc
}

task configZip(type: Zip) {
    from openZWaveConfigDir
    classifier "ozw_config"
}

task wrapper(type: Wrapper) {
    gradleVersion = "1.10"
}

artifacts {
    archives sourcesJar
    archives javadocJar
    archives configZip
}

signing {
    required { signEnabled }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { signing.signPom(it) }

            repository(url: "https://oss.sonatype.org/" + (release ? "service/local/staging/deploy/maven2" : "content/repositories/snapshots")) {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                packaging "jar"
                name "ZWave4J"
                description "ZWave4J is a Java wrapper for Open-ZWave library"
                url "https://github.com/zgmnkv/zwave4j"

                scm {
                    url "https://github.com/zgmnkv/zwave4j"
                    connection "scm:git:https://github.com/zgmnkv/zwave4j"
                    developerConnection "scm:git:https://github.com/zgmnkv/zwave4j"
                }

                licenses {
                    license {
                        name "GNU GENERAL PUBLIC LICENSE, Version 3, 29 June 2007"
                        url "http://www.gnu.org/licenses/gpl-3.0.txt"
                        distribution "repo"
                    }
                }

                developers {
                    developer {
                        id "zgmnkv"
                        name "Alexander Zagumennikov"
                    }
                }
            }
        }
    }
}
