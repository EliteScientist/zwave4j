apply plugin: 'maven'
apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'idea'
apply plugin: 'cpp-lib'

group = 'org.zwave4j'
version = '0.1'
mainClassName = 'org.zwave4j.Main'

ext {
    openZwaveDir = project.openZwaveDir
    openZwaveLibFile = "${project.openZwaveDir}/${project.openZwaveLibFile}"

    javaHome = System.getenv('JAVA_HOME')
    nativeClasses = ['org.zwave4j.Manager', 'org.zwave4j.Options']
    nativeLibsDir = 'native-libs'

    def osName = System.properties["os.name"]
    if (osName == "Linux") {
        os = "linux"
    } else if (osName.startsWith("Win")) {
        os = "windows"
    } else if (osName == "SunOS"){
        os = "solaris"
    } else if (osName == "Mac OS X") {
        os = "mac_os_x"
    }

    def architectureName = System.properties["os.arch"]
    if (architectureName == "i386" || architectureName == "i686") {
        architecture = "x86"
    } else if (architectureName == "amd64") {
        architecture = "amd64"
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

libraries {
	main {
        spec {
            includes(files("${javaHome}/include"))
            includes(files("${javaHome}/include/win32"))
            includes(files("${openZwaveDir}/cpp/src"))
            includes(files("${openZwaveDir}/cpp/src/value_classes"))
            includes(files("${openZwaveDir}/cpp/src/command_classes"))
            includes(files("${openZwaveDir}/cpp/src/platform"))
            includes(files("${openZwaveDir}/cpp/src/platform/unix"))
            includes(files("${openZwaveDir}/cpp/src/platform/windows"))
            args openZwaveLibFile
            args "-lsetupapi"
            args (architecture == "amd64" ? "-m64" : (architecture == "x86" ? "-m32" : ""))
        }
    }
}

run {
    standardInput = System.in
    jvmArgs = [
            "-Xdebug",
            "-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=5005",
            "-Djava.library.path=\"${buildDir}/${nativeLibsDir}\""
    ]
}

task generateNativeHeaders(type: Exec, dependsOn: classes) {
    commandLine 'javah'
    args '-d', 'src/main/headers'
    args '-classpath', sourceSets.main.runtimeClasspath.asPath
    args nativeClasses
}

compileMain.dependsOn generateNativeHeaders

task copyNativeLibs(type: Copy, dependsOn: compileMain) {
    from "${buildDir}/binaries"
    into "${buildDir}/${nativeLibsDir}/${os}/${architecture}"
}

run.dependsOn copyNativeLibs

installApp {
    dependsOn copyNativeLibs
    from "${buildDir}/${nativeLibsDir}", {
        into "lib/${nativeLibsDir}"
    }
}

task nativeLibsZip(type: Zip, dependsOn: copyNativeLibs) {
    from "${buildDir}/${nativeLibsDir}"
    classifier = 'native_libs'
}

task srcJar(type: Jar) {
    from "src/main/java"
    classifier = 'sources'
}

artifacts {
    archives nativeLibsZip
    archives srcJar
}
